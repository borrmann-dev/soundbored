defmodule Soundboard.FavoritesTest do
  @moduledoc """
  Test for the Favorites module.
  """
  use Soundboard.DataCase
  alias Soundboard.{Accounts.User, Favorites, Sound}

  describe "favorites" do
    setup do
      user = insert_user()
      sound = insert_sound(user)
      %{user: user, sound: sound}
    end

    test "list_favorites/1 returns all favorites for a user", %{user: user, sound: sound} do
      {:ok, _favorite} = Favorites.toggle_favorite(user.id, sound.id)
      assert [sound.id] == Favorites.list_favorites(user.id)
    end

    test "toggle_favorite/2 adds a favorite when it doesn't exist", %{user: user, sound: sound} do
      assert {:ok, favorite} = Favorites.toggle_favorite(user.id, sound.id)
      assert favorite.user_id == user.id
      assert favorite.sound_id == sound.id
    end

    test "toggle_favorite/2 removes a favorite when it exists", %{user: user, sound: sound} do
      {:ok, _favorite} = Favorites.toggle_favorite(user.id, sound.id)
      {:ok, deleted_favorite} = Favorites.toggle_favorite(user.id, sound.id)
      assert deleted_favorite.__meta__.state == :deleted
      assert [] == Favorites.list_favorites(user.id)
    end

    test "favorite?/2 returns true when favorite exists", %{user: user, sound: sound} do
      refute Favorites.favorite?(user.id, sound.id)
      {:ok, _favorite} = Favorites.toggle_favorite(user.id, sound.id)
      assert Favorites.favorite?(user.id, sound.id)
    end

    test "can add unlimited favorites", %{user: user} do
      # Create 50 sounds to verify no limit exists
      sounds = Enum.map(1..50, fn _ -> insert_sound(user) end)

      # All should be added successfully
      Enum.each(sounds, fn sound ->
        assert {:ok, _} = Favorites.toggle_favorite(user.id, sound.id)
      end)

      assert length(Favorites.list_favorites(user.id)) == 50
    end
  end

  # Helper functions
  defp insert_user(attrs \\ %{}) do
    {:ok, user} =
      %User{}
      |> User.changeset(
        Map.merge(
          %{
            username: "testuser",
            discord_id: "123456789",
            avatar: "test_avatar.jpg"
          },
          attrs
        )
      )
      |> Repo.insert()

    user
  end

  defp insert_sound(user, attrs \\ %{}) do
    {:ok, sound} =
      %Sound{}
      |> Sound.changeset(
        Map.merge(
          %{
            filename: "test_sound#{System.unique_integer()}.mp3",
            source_type: "local",
            user_id: user.id
          },
          attrs
        )
      )
      |> Repo.insert()

    sound
  end
end
